set(NOVELRT_SAMPLES_HEADERS)
source_group(TREE ${CMAKE_SOURCE_DIR} FILES ${NOVELRT_SAMPLES_HEADERS})

file(GLOB_RECURSE NOVELRT_SAMPLES_SOURCES CONFIGURE_DEPENDS *.cpp)
source_group(TREE ${CMAKE_SOURCE_DIR} FILES ${NOVELRT_SAMPLES_SOURCES})

set(NOVELRT_SAMPLES_LINK_LIBRARIES
  NovelRT
)

add_executable(NovelRTSamples ${NOVELRT_SAMPLES_HEADERS} ${NOVELRT_SAMPLES_SOURCES})
add_dependencies(NovelRTSamples NovelRTSamplesDependencies)

target_link_libraries(NovelRTSamples ${NOVELRT_SAMPLES_LINK_LIBRARIES})

add_custom_command(TARGET NovelRTSamples POST_BUILD
  COMMAND ${CMAKE_COMMAND} -E touch ${CMAKE_CURRENT_BINARY_DIR}/NovelRTSamples.txt
)

set_property(TARGET NovelRTSamples PROPERTY
  BINARY_OUTDIR $<TARGET_FILE_DIR:NovelRTSamples>
)

# NovelRTSamples.Dependencies

set(NOVELRT_SAMPLES_DEPENDENCIES_INPUTS
  $<TARGET_FILE:NovelRT>
  ${CMAKE_BINARY_DIR}/src/NovelRTDotNet.txt
  ${CMAKE_BINARY_DIR}/src/NovelRTResources.txt
)

set(NOVELRT_SAMPLES_DEPENDENCIES_OUTPUTS
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRT.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTDotNet.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTResources.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTSamplesDependencies.txt
)

add_custom_command(OUTPUT ${NOVELRT_SAMPLES_DEPENDENCIES_OUTPUTS}
  COMMAND ${CMAKE_COMMAND} -E copy_directory $<TARGET_FILE_DIR:NovelRT> $<TARGET_GENEX_EVAL:NovelRTSamples,$<TARGET_PROPERTY:NovelRTSamples,BINARY_OUTDIR>>
  COMMAND ${CMAKE_COMMAND} -E touch ${NOVELRT_SAMPLES_DEPENDENCIES_OUTPUTS}
  DEPENDS ${NOVELRT_SAMPLES_DEPENDENCIES_INPUTS}
)

add_custom_target(NovelRTSamplesDependencies ALL DEPENDS ${NOVELRT_SAMPLES_DEPENDENCIES_OUTPUTS})
