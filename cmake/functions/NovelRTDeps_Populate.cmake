macro(NovelRTDeps_PopulateHeaderOnly)
  foreach(NAME IN ITEMS ${ARGV})
    string(TOLOWER ${NAME} lowername)
    FetchContent_GetProperties(${NAME})
    if(NOT ${lowername}_POPULATED)
      FetchContent_Populate(${NAME})
      set(${lowername}_SOURCE_DIR ${${lowername}_SOURCE_DIR} CACHE STRING "${NAME} Source Directory" FORCE)
      set(${lowername}_BINARY_DIR ${${lowername}_BINARY_DIR} CACHE STRING "${NAME} Binary Directory" FORCE)
    endif()
  endforeach()
endmacro()

macro(NovelRTDeps_Populate NAME)
  string(TOLOWER ${NAME} lowername)
  FetchContent_GetProperties(${NAME})
  if(NOT ${lowername}_POPULATED)
    FetchContent_Populate(${NAME})
    add_subdirectory(${${lowername}_SOURCE_DIR} ${${lowername}_BINARY_DIR} EXCLUDE_FROM_ALL)
  endif()
endmacro()

macro(NovelRTDeps_PopulateExplicit NAME TYPE)
  string(TOLOWER ${TYPE} lowertype)
  if(lowertype MATCHES "static")
    set(BUILD_SHARED_LIBS OFF)
  else()
    set(BUILD_SHARED_LIBS ON)
  endif()
  string(TOLOWER ${NAME} lowername)
  FetchContent_GetProperties(${NAME})
  if(NOT ${lowername}_POPULATED)
    FetchContent_Populate(${NAME})
    add_subdirectory(${${lowername}_SOURCE_DIR} ${${lowername}_BINARY_DIR} EXCLUDE_FROM_ALL)
  endif()
endmacro()
