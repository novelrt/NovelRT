set(NOVELRT_LUA_SAMPLES_HEADERS)
source_group(TREE ${CMAKE_SOURCE_DIR} FILES ${NOVELRT_LUA_SAMPLES_HEADERS})

file(GLOB_RECURSE NOVELRT_LUA_SAMPLES_SOURCES CONFIGURE_DEPENDS *.cpp)
source_group(TREE ${CMAKE_SOURCE_DIR} FILES ${NOVELRT_LUA_SAMPLES_SOURCES})

set(NOVELRT_LUA_SAMPLES_LINK_LIBRARIES
  NovelRT
)

add_executable(NovelRTLuaSamples ${NOVELRT_LUA_SAMPLES_HEADERS} ${NOVELRT_LUA_SAMPLES_SOURCES})
add_dependencies(NovelRTLuaSamples NovelRTLuaSamplesDependencies)

target_link_libraries(NovelRTLuaSamples ${NOVELRT_LUA_SAMPLES_LINK_LIBRARIES})

add_custom_command(TARGET NovelRTLuaSamples POST_BUILD
  COMMAND ${CMAKE_COMMAND} -E touch ${CMAKE_CURRENT_BINARY_DIR}/NovelRTLuaSamples.txt
)

set_property(TARGET NovelRTLuaSamples PROPERTY
  BINARY_OUTDIR $<TARGET_FILE_DIR:NovelRTLuaSamples>
)

# NovelRTLuaSamples.Dependencies

set(NOVELRT_LUA_SAMPLES_DEPENDENCIES_INPUTS
  $<TARGET_FILE:NovelRT>
  ${CMAKE_BINARY_DIR}/src/NovelRTDotNet.txt
  ${CMAKE_BINARY_DIR}/src/NovelRTResources.txt
)

set(NOVELRT_LUA_SAMPLES_DEPENDENCIES_OUTPUTS
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRT.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTDotNet.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTResources.txt
  ${CMAKE_CURRENT_BINARY_DIR}/NovelRTLuaSamplesDependencies.txt
)

add_custom_command(OUTPUT ${NOVELRT_LUA_SAMPLES_DEPENDENCIES_OUTPUTS}
  COMMAND ${CMAKE_COMMAND} -E copy_directory $<TARGET_FILE_DIR:NovelRT> $<TARGET_GENEX_EVAL:NovelRTLuaSamples,$<TARGET_PROPERTY:NovelRTLuaSamples,BINARY_OUTDIR>>
  COMMAND ${CMAKE_COMMAND} -E touch ${NOVELRT_LUA_SAMPLES_DEPENDENCIES_OUTPUTS}
  DEPENDS ${NOVELRT_LUA_SAMPLES_DEPENDENCIES_INPUTS}
)

add_custom_target(NovelRTLuaSamplesDependencies ALL DEPENDS ${NOVELRT_LUA_SAMPLES_DEPENDENCIES_OUTPUTS})
